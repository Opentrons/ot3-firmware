
find_package(GDBSVDTools)

set(G4_BOARD_FILE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/stm32g4discovery.cfg)

# Fills in the template with values specified by the find_package(OpenOCD) call above
configure_file(./gdbinit.template ./gdbinit)

add_library(
    STM32G491RETx STATIC
    ./startup_stm32g491xx.s
)

target_link_options(STM32G491RETx
  INTERFACE
  "LINKER:-T,${CMAKE_CURRENT_SOURCE_DIR}/STM32G491RETx_FLASH.ld"
  "LINKER:--print-memory-usage"
  "LINKER:--error-unresolved-symbols")

# Incurs at least a relink when you change the linker file (and a recompile of main
# but hopefully that's quick)
set_source_files_properties(./startup_stm32g491xx.s
  PROPERTIES
  OBJECT_DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/STM32G491RETx_FLASH.ld)


# A function to generate a flash command that will flash an subsystem's bootloader and application firmware
# PREFIX - This is the name of the subsystem. `PREFIX-flash` is the target generated by this function
# APP_BIN - Full path of application bin file
# BOOTLOADER_BIN - Full path of the bootloader bin file
function(target_stm32G4_flash PREFIX APP_BIN BOOTLOADER_BIN)
    add_custom_target(${PREFIX}-flash
            COMMAND "${OpenOCD_EXECUTABLE}" "-f" "${COMMON_EXECUTABLE_DIR}/STM32G491RETx/stm32g4discovery.cfg" "-c" "init;reset init;halt;stm32g4x mass_erase 0;flash write_image ${BOOTLOADER_BIN} 0x08000000;halt;flash write_image ${APP_BIN} 0x08008000;reset;exit"
            VERBATIM
            COMMENT "Flashing board"
            DEPENDS ${APP_BIN} ${BOOTLOADER_BIN})
endfunction()
